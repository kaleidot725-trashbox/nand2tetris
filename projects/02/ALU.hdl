// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
        // 入力 X をゼロにする
        Mux16(a=x, b=false, sel=zx, out=zeroX);

        // 入力 X を反転する
        Not16(in=zeroX, out=notZeroX);
        Mux16(a=zeroX, b=notZeroX, sel=nx, out=finalX);

        // 入力 Y をゼロにする
        Mux16(a=y, b=false, sel=zy, out=zeroY);

        // 入力 Y を反転する
        Not16(in=zeroY, out=notZeroY);
        Mux16(a=zeroY, b=notZeroY, sel=ny, out=finalY);

        // 足し算 or AND演算を実行する
        And16(a=finalX, b=finalY, out=and);
        Add16(a=finalX, b=finalY, out=sum);        

        // 制御方式に応じて出力する結果を変える
        Mux16(a=and, b=sum, sel=f, out=calcOut);

        // 結果を反転する
        Not16(in=calcOut, out=notCalcOut);

        // 制御方式に応じて出力する結果を変える
        Mux16(a=calcOut, b=notCalcOut, sel=no, out=out, out[15]=out15, out[0..7]=out0-7, out[8..15]=out8-15);

        // ゼロ比較
        Or8Way(in=out0-7, out=or-out0-7);
        Or8Way(in=out8-15, out=or-out8-15);
        Or(a=or-out0-7, b=or-out8-15, out=or-out0-15);
        Not(in=or-out0-15, out=zr);

        // ゼロ以下であるか
        And(a=out15, b=true, out=ng);
}